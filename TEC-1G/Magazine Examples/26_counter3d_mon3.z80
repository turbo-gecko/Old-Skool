; 000-999 Counter
; Use '+' key to increment
; Use '-' key to decrement
; Use 'AD' key to clear

	ORG	4000H
SCREEN:	EQU	0D00H

	LD	HL,SCREEN+3
	XOR	A
	LD	(HL),A
	INC	HL
	LD	(HL),A
START:
	LD	BC,SCREEN
	LD	DE,SCREEN+3
	LD	A,(DE)
	CALL	BITE2N
	INC	DE
	LD	A,(DE)
	CALL	NIBBLE
	LD	HL,SCREEN+2
	CALL	MULTIPLEX
	LD	HL,SCREEN+3

	PUSH	BC
	LD	C,10H
	RST	10H
	POP	BC

INCREMENT:
	CP	10H
	JR	NZ,DECREMENT
	CALL	DELAY
	LD	A,(HL)
	INC	A
	DAA
	LD	(HL),A
	JR	NC,CLEAR
	INC	HL
	LD	A,(HL)
	INC	A
	DAA
	LD	(HL),A
	JR	CLEAR
DECREMENT:
	CP	11H
	JR	NZ,RESET
	CALL	DELAY
	LD	A,(HL)
	DEC	A
	DAA
	LD	(HL),A
	JR	NC,CLEAR
	INC	HL
	LD	A,(HL)
	DEC	A
	DAA
	LD	(HL),A
	JR	CLEAR
RESET:
	CP	13H
	JR	NZ,CLEAR
	CALL	DELAY
	XOR	A
	LD	(HL),A
	INC	HL
	LD	(HL),A
CLEAR:
	LD	A,0FFH
	JR	START
BITE2N:
	PUSH	AF
	CALL	NIBBLE
	POP	AF
	RRA
	RRA
	RRA
	RRA
	CALL	NIBBLE
	RET
NIBBLE:
	AND	0FH
	LD	HL,TABLE
	ADD	A,L
	LD	L,A
	LD	A,(HL)
	LD	(BC),A
	INC	BC
	RET
MULTIPLEX:
	LD	B,04H
LOOP1:
	LD	A,(HL)
	OUT	(02),A
	LD	A,B
	OUT	(01),A
	LD	B,50H
LOOP2:
	DJNZ	LOOP2
	DEC	HL
	LD	B,A
	XOR	A
	OUT	(01),A
	RRC	B
	JR	NC,LOOP1
	RET
DELAY:
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	LD	HL,4000H      ; Time delay of ~100mS
	LD	C,21H
	RST	10H
	POP	AF
	POP	BC
	POP	DE
	POP	HL
	RET
TABLE:
	DB	0EBH,28H,0CDH,0ADH,2EH,0A7H,0E7H,29H,0EFH,0AFH